apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.25.1'
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'

    implementation 'com.google.code.gson:gson:2.8.2'

    implementation 'com.squareup.okhttp3:okhttp:3.9.1'

    // retrofit
    implementation('com.squareup.retrofit2:retrofit:2.3.0') {
        exclude module: 'okhttp'
    }
    implementation 'com.squareup.retrofit2:converter-scalars:2.3.0'

    // image loader
    implementation 'com.github.bumptech.glide:glide:4.4.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.4.0'
    implementation('com.github.bumptech.glide:okhttp-integration:4.4.0') {
        //exclude group: 'com.squareup.okhttp3', module: 'okhttp'
        transitive = false
    }

    implementation 'net.hockeyapp.android:HockeySDK:5.0.4'
    implementation 'com.tencent.bugly:crashreport:2.6.6'

    implementation 'com.facebook.stetho:stetho:1.5.0'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.4@aar') {
        transitive = true;
    }
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "site.linyuange.awesome.splash"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "0.0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    def FABRIC_ID, HOCKEY_APP_ID
    buildTypes.each {
        def buildType = 'debug'.equals(it.name) ? 'debug' : it.name

        Properties props = new Properties()
        def propFile = file("../config-${buildType}.properties")

        if (propFile.canRead()) {
            props.load(new FileInputStream(propFile))
            if (props != null) {
                FABRIC_ID = props['FABRIC_ID']
                HOCKEY_APP_ID = props['HOCKEY_APP_ID']

                props.each { prop ->
                    it.buildConfigField("String", prop.key, "\"${prop.value}\"")
                }
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            resValue "string", "app_name", "Debug Awesome Splash"
            manifestPlaceholders = [FABRI_ID     : FABRIC_ID,
                                    HOCKEY_APP_ID: HOCKEY_APP_ID]
        }
        release {
            resValue "string", "app_name", "Awesome Splash"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [FABRI_ID     : FABRIC_ID,
                                    HOCKEY_APP_ID: HOCKEY_APP_ID]
        }
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}
